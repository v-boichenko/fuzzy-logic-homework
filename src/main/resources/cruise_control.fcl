FUNCTION_BLOCK CruiseControl

VAR_INPUT
    speed : REAL;
    distance : REAL;
END_VAR

VAR_OUTPUT
    throttle : REAL;
END_VAR

// 0 - 120 km/h
FUZZIFY speed
    TERM slow := (0, 1) (30, 1) (60, 0);
    TERM medium := (40, 0) (70, 1) (100, 0);
    TERM fast := (80, 0) (110, 1) (120, 1);
END_FUZZIFY

FUZZIFY distance
    TERM very_close := (0, 1) (2, 1) (3, 0);
    TERM close := (2, 0) (40, 1) (70, 0);
    TERM safe := (50, 0) (100, 1) (150, 0);
    TERM far := (120, 0) (170, 1) (200, 1);
END_FUZZIFY

 // [-100%, 100%], -10m/s**2 - 10m/s**2
DEFUZZIFY throttle
    TERM full_brake := (-100, 1) (-70, 0);
    TERM medium_brake := (-85, 0) (-60, 1) (-35, 0);
    TERM decelerate := (-50, 0) (-20, 1) (0, 0);
    TERM maintain := (-20, 0) (0, 1) (30, 0);
    TERM accelerate := (20, 0) (50, 1) (80, 0);
    TERM max_accelerate := (70, 0) (90, 1) (100, 1);
    METHOD : LM;
    DEFAULT := -25;
END_DEFUZZIFY

RULEBLOCK ACC_Rules
    AND : MIN;
    ACT : MIN;
    ACCU : MAX;

    // VERY_CLOSE - critical
    RULE 1 : IF distance IS very_close AND speed IS slow THEN throttle IS full_brake;

    // CLOSE - brake
    RULE 2 : IF distance IS close AND speed IS slow THEN throttle IS medium_brake;
    RULE 3 : IF distance IS close AND speed IS medium THEN throttle IS medium_brake;
    RULE 4 : IF distance IS close AND speed IS fast THEN throttle IS full_brake;

    // 3. MEDIUM - keep the distance
    RULE 5 : IF distance IS safe AND speed IS slow THEN throttle IS accelerate;
    RULE 6 : IF distance IS safe AND speed IS medium THEN throttle IS maintain;
    RULE 7 : IF distance IS safe AND speed IS fast THEN throttle IS decelerate;

    // 4. FAR - maintain high speed
    RULE 8 : IF distance IS far AND speed IS slow THEN throttle IS max_accelerate;
    RULE 9 : IF distance IS far AND speed IS medium THEN throttle IS accelerate;
    RULE 10 : IF distance IS far AND speed IS fast THEN throttle IS maintain;
END_RULEBLOCK

END_FUNCTION_BLOCK